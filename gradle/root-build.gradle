// root build.gradle
buildscript {
  repositories {
    google()
    jcenter()
    maven("https://jitpack.io")
  }
  dependencies {
    classpath Libs.com_android_tools_build_gradle
    classpath Libs.kotlin_gradle_plugin
  }
}

plugins {
    id("com.gradle.build-scan") version "1.16"
    id("jmfayard.github.io.gradle-kotlin-dsl-libs") version "0.2.6"
    id("org.jlleitschuh.gradle.ktlint") version "6.3.0" apply (false)
}

// for Android: run the unit tests, assemble the default build variant, install the app and open the default activity
// $ ./gradlew install
tasks.register<Exec>("install") {
    dependsOn(":app:testDebugUnitTest", ":app:installDebug")
    group = "Custom"
    commandLine = "adb shell am start -n ${Config.applicationId}/${Config.activity} -a android.intent.action.MAIN -c android.intent.category.LAUNCHER".split(" ")
}

allprojects {
  apply(plugin = "org.jlleitschuh.gradle.ktlint")

  repositories {
    google()
    jcenter()
    maven("https://jitpack.io")
  }

  configurations.all {
    resolutionStrategy {
        for (version in Config.forceDependencyVersions) {
            force(version)
        }
    }
  }
}


// Update Gradle with    $ ./gradlew syncLibs && ./gradlew wrapper 
tasks.withType<Wrapper> {
    group = "Custom"
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = Versions.Gradle.currentVersion
}

buildScan {
    setTermsOfServiceUrl("https://gradle.com/terms-of-service")
    setTermsOfServiceAgree("yes")
    publishAlways()
}



